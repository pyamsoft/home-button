/*
 * Copyright 2013 - 2016 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

tasks.whenTaskAdded { task ->
  if (task.name.startsWith("lint")) {
    task.enabled = false
  }
}

apply plugin: "com.android.application"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "net.ltgt.errorprone"
apply plugin: "me.tatarka.retrolambda"

android {
  compileSdkVersion 24
  buildToolsVersion "$buildTools"

  defaultConfig {
    applicationId "com.pyamsoft.homebutton"
    minSdkVersion 16
    targetSdkVersion 24
    versionCode 36
    versionName "3.3.2"
    // For vector drawables
    vectorDrawables.useSupportLibrary = true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    release {
      storeFile new File(RELEASE_STORE_FILE)
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWD
      storePassword RELEASE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
          "proguard-rules.txt"
    }
  }

  dexOptions {
    javaMaxHeapSize "$heapSize"
  }
}

dependencies {
  compile fileTree(dir: "libs", include: ["*.jar"])

  retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.3.0"

  // Error-Prone compiler
  errorprone "com.google.errorprone:error_prone_core:$errorProne"

  // Needs to compile support annotations into actual project
  compile "com.android.support:support-annotations:$supportLib"

  // Firebase Crash and Analytics
  compile "com.google.firebase:firebase-crash:$firebase"

  // Butterknife
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife"

  // PYDroid 4.4.2
  debugCompile project(path: ":PYDroid", configuration: "debug")
  releaseCompile project(path: ":PYDroid", configuration: "release")
}

// AdView
apply plugin: "com.google.gms.google-services"

